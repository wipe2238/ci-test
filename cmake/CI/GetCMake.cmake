cmake_minimum_required( VERSION 2.8.7 ) # oldest preinstalled (travis)

if( NOT $ENV{CI} )
	message( FATAL_ERROR "CI not set" )
endif()

if( $ENV{APPVEYOR} )
	message( STATUS "Running on AppVeyor" )
	set( CMFILE "C:\\cmake.zip" )
#	set( CMFILE "/tmp/cmake.zip" )
	set( CMSUFFIX "win32-x86.zip" )
elseif( $ENV{TRAVIS} )
	message( STATUS "Running on Travis" )
	set( CMFILE "/tmp/cmake.sh" )
	set( CMSUFFIX "Linux-x86_64.sh" )
else()
	message( FATAL_ERROR "Unknown CI" )
endif()

if( NOT CMAKELISTS )
	message( FATAL_ERROR "CMAKELISTS not set" )
elseif( NOT CMDIR )
	message( FATAL_ERROR "CMDIR not set" )
endif()

message( STATUS "Checking required CMake version" )
file( STRINGS ${CMAKELISTS} CMVERSION REGEX "^[\ \t]*cmake_minimum_required" )
string( REGEX REPLACE ".*VERSION[ \t]+([0-9]+\\.[0-9]+).*" "\\1" CMVERS ${CMVERSION} )
string( REGEX REPLACE ".*VERSION[ \t]+([0-9]+\\.[0-9]+\\.[0-9]+).*" "\\1" CMVERL ${CMVERSION} )

if( CMAKE_VERSION VERSION_EQUAL CMVERL )#OR CMAKE_VERSION VERSION_GREATER CMVERL )
	message( STATUS "Installed CMake meets requirements" )
	return()
endif()

set( CMURL "http://cmake.org/files/v${CMVERS}/cmake-${CMVERL}-${CMSUFFIX}" )
message( STATUS "Downloading ${CMURL}" )
file( DOWNLOAD "${CMURL}" ${CMFILE} SHOW_PROGRESS )

message( STATUS "Extracting ${CMFILE}" )
file( MAKE_DIRECTORY ${CMDIR} )
if( $ENV{APPVEYOR} )
	execute_process( COMMAND 7z x ${CMFILE} -o${CMDIR}.tmp )
	file( RENAME ${CMDIR}.tmp/cmake-${CMVERL}-win32-x86 ${CMDIR} )
elseif( $ENV{TRAVIS} )
	execute_process( COMMAND sh ${CMFILE} --prefix=${CMDIR} --exclude-subdir )
endif()
