cmake_minimum_required( VERSION 2.8.7 ) # oldest preinstalled (travis)

if( NOT $ENV{CI} )
	message( FATAL_ERROR "CI" )
endif()

if( $ENV{APPVEYOR} )
	message( STATUS "Running on AppVeyor" )
	set( CMFILE "$ENV{TMP}\\cmake.zip" )
#	set( CMFILE "/tmp/cmake.zip" )
	set( CMSUFFIX "win32-x86.zip" )
elseif( $ENV{TRAVIS} )
	message( STATUS "Running on Travis" )
	set( CMFILE "/tmp/cmake.sh" )
	set( CMSUFFIX "Linux-x86_64.sh" )
else()
	message( FATAL_ERROR "Unknown CI" )
endif()

if( NOT CMAKELISTS )
	message( FATAL_ERROR "CMAKELISTS not set" )
elseif( NOT CMAKEDIR )
	message( FATAL_ERROR "CMAKEDIR not set" )
endif()

message( STATUS "Installed CMake -- v${CMAKE_VERSION}" )
file( STRINGS ${CMAKELISTS} CMVERSION REGEX "^[\ \t]*cmake_minimum_required" )
string( REGEX REPLACE ".*VERSION[ \t]+([0-9]+\\.[0-9]+).*" "\\1" CMVERS ${CMVERSION} )
string( REGEX REPLACE ".*VERSION[ \t]+([0-9]+\\.[0-9]+\\.[0-9]+).*" "\\1" CMVERL ${CMVERSION} )

if( CMAKE_VERSION VERSION_LESS CMVERL )
	message( STATUS "Downloading CMake -- v${CMVERL}" )
	file( DOWNLOAD "http://cmake.org/files/v${CMVERS}/cmake-${CMVERL}-${CMSUFFIX}" ${CMFILE} )

	message( STATUS "Extracting CMake -- ${CMAKEDIR}" )
	if( $ENV{APPVEYOR} )
		execute_process( COMMAND 7z x ${CMFILE} -o${CMAKEDIR}.tmp OUTPUT_QUIET )
		file( RENAME "${CMAKEDIR}.tmp\\cmake-${CMVERL}-win32-x86" ${CMAKEDIR} )
	elseif( $ENV{TRAVIS} )
		file( MAKE_DIRECTORY ${CMAKEDIR} )
		execute_process( COMMAND sh ${CMFILE} --prefix=${CMAKEDIR} --exclude-subdir OUTPUT_QUIET )
	endif()
endif()
